apply plugin: 'eclipse'
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'



group = 'net.filebot'
archivesBaseName = 'ant-spk'
version = '0.5'



task javadocJar(type: Jar) {
	classifier = 'javadoc'
	from javadoc
}

task sourcesJar(type: Jar) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

artifacts {
	archives javadocJar, sourcesJar
}

if (project.hasProperty('signing.keyId')) {
	signing {
		sign configurations.archives
	}
}



repositories {
	mavenCentral()
}

dependencies {
	compileOnly 'org.apache.ant:ant:1.9.7'
	compile 'org.bouncycastle:bcprov-jdk15on:1.55'
	compile 'org.bouncycastle:bcpg-jdk15on:1.55'
	compile 'org.apache.httpcomponents:httpcore:4.4.5'
	compile 'org.apache.httpcomponents:httpmime:4.5.2'
	compile 'org.apache.httpcomponents:httpclient:4.5.2'
	compile 'org.glassfish:javax.json:1.0.4'
	compile 'org.tukaani:xz:1.5'
}



if (project.hasProperty('ossrhUsername')) {
	uploadArchives {
		repositories {
			mavenDeployer {
				beforeDeployment { MavenDeployment deployment ->
					signing.signPom(deployment)
				}

				repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
					authentication(userName: ossrhUsername, password: ossrhPassword)
				}

				snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
					authentication(userName: ossrhUsername, password: ossrhPassword)
				}

				pom.project {
					name 'ant-spk'
					packaging 'jar'
					description 'Ant Task for creating SPK packages for Synology NAS'
					url 'https://github.com/rednoah/ant-spk'

					scm {
						url 'https://github.com/rednoah/ant-spk'
						connection 'scm:git:git://github.com/rednoah/ant-spk.git'
						developerConnection 'scm:git:git@github.com:rednoah/ant-spk.git'
					}

					licenses {
						license {
							name 'The Apache License, Version 2.0'
							url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
						}
					}

					developers {
						developer {
							id 'rednoah'
							name 'Reinhard Pointner'
							email 'rednoah@filebot.net'
						}
					}
				}
			}
		}
	}
}



task example(type: Exec, dependsOn: build) {
	workingDir 'examples/helloworld'
	executable 'ant'
	args configurations.runtime.plus(libsDir).collectMany{ jar -> ['-lib', jar] }
}
